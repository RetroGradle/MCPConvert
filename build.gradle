plugins {
    id 'application'
}

group = 'uk.gemwire'
archivesBaseName = 'mcpconvert'
version = '0.1'

repositories {
    mavenCentral()
    jcenter()
    maven { url = 'https://files.minecraftforge.net/maven/' }
}

dependencies {
    implementation group: 'net.minecraftforge', name: 'srgutils', version: '0.3.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.11.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2'
    implementation group: 'com.google.jimfs', name: 'jimfs', version: '1.2'

    testImplementation(platform('org.junit:junit-bom:5.7.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

application {
    mainClassName = 'uk.gemwire.mcpconvert.Main'
    applicationDefaultJvmArgs += ["--enable-preview"]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
    }
}

// Enable preview features, so we can have records and such

tasks.withType(JavaCompile) {
    options.compilerArgs += '--enable-preview'
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}